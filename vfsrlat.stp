#!/usr/bin/env stap
/*
 * vfsrlat.stp	Summarize VFS read latency for different FS types.
 *
 * USAGE: ./vfsrlat.stp		# Ctrl-C to end
 *
 * This prints a histogram so that the presense of latency outliers can be
 * identified.
 *
 * WARNING: This is known to crash systems.
 *
 * COPYRIGHT: Copyright (c) 2011 Brendan Gregg.
 *
 * CDDL HEADER START
 *
 *  The contents of this file are subject to the terms of the
 *  Common Development and Distribution License, Version 1.0 only
 *  (the "License").  You may not use this file except in compliance
 *  with the License.
 *
 *  You can obtain a copy of the license at Docs/cddl1.txt
 *  or http://www.opensolaris.org/os/licensing.
 *  See the License for the specific language governing permissions
 *  and limitations under the License.
 *
 * CDDL HEADER END
 */

global start;
global latency;

probe begin
{
	println("Tracing... Hit Ctrl-C to end");
}

probe vfs.read
{
	start[tid()] = gettimeofday_ns();
}

probe vfs.read.return
{
	if (start[tid()]) {
		fstypep = $file->f_path->dentry->d_inode->i_sb->s_type->name;
		latency[kernel_string(fstypep)] <<<
		    gettimeofday_ns() - start[tid()];
		delete start[tid()];
	}
}

probe end
{
	foreach (fs in latency+) {
		printf("   %s (ns):\n", fs);
		print(@hist_log(latency[fs]));
	}
}
